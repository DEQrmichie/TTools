' Name:  View.R2Vpoly
' 
' Title:  Converts a grid theme to a polygon shapefile theme.
'
' Topics:  Raster to Vector Conversion
'
' Description:  Takes an Grid theme from the active view and converts it to a line shapefile.
' It makes use of the asPolyLineFtab function request. The grid needs to consist of polygons.
'
' Requires:  A View with one active Grid Theme.  
'
' Self:  
'
' Returns:  

'av.finddialog("grid2poly").close

theView = av.GetActiveDoc
theproj = theView.getprojection

thefilename = FileDialog.Put("NewFile.shp".asfilename,"*.shp","Enter the new output filename.")
 
 If (thefilename = nil) then 
   exit 
 end
 
 anFN = thefilename.asstring
 position = anFN.IndexOf(".")
 if (position = -1)
   then
     thefilename = (anFN + ".shp").asfilename
   else
     anFN = anFN.Left(position -1)
     thefilename = (anFN + ".shp").asfilename
 end
 


weed = msgbox.YesNo("If you choose YES for Perform Weeding, then a generalization algorithm tailored for raster to vector conversion is applied. If you choose NO, then no weeding is done. The generalization algorithm uses the Douglas-Poiker algorithm with a tolerance of: sqrt(0.5) * cellsize."
,"Perform Weeding",true)

if (theproj.isnull.not)
  then
    keepprj = msgbox.YesNo("Do you want the new shape file to remain in the current map projection? If you choose yes then it will be saved in the current projection. If you choose No then it will be converted to decimal degrees","Projection",true)
  else
    keepprj = true  
end

for each t in theView.GetActiveThemes
  if (t.Is(GTHEME)) then
    ' get grid and display source name
    theGrid = t.GetGrid
    if (keepprj = true)
      then
        lineResult = theGrid.AsPolygonFTab(thefilename, weed, Prj.makenull)
      else
        lineResult = theGrid.AsPolygonFTab (thefilename,weed,theproj)
    end

  end
end




if (MsgBox.YesNo("Add shapefile as theme to a view?",
  "R2Vpoly", true).Not) then
  exit
end
 
 
viewList = {}
for each d in av.GetProject.GetDocs
  if (d.Is(View)) then
    viewList.Add( d )
  end
end
 
viewList.Add("<New View>")
 
addToView = MsgBox.ListAsString( viewList,"Add Theme to:", "R2Vpoly" )
 
 
if (addToView <> nil) then
 
  if (addToView = "<New View>") then
    addToView = View.Make
    addToView.GetWin.Open
  end
   
  theFTheme = FTheme.Make(lineResult) 
  addToView.AddTheme(theFtheme)
 
    addToView.GetWin.Activate
end







